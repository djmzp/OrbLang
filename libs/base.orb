eval (sym (base.-genSymToken 0:u64));

eval (fnc genSym () id {
    sym (s (+ \base.-genSym (cast id base.-genSymToken)));
    = base.-genSymToken (+ base.-genSymToken 1);
    ret s;
});

mac passthrough (x::preprocess) {
    ret x;
};

mac if (cond then) {
    ret \(block {
        (exit (! ,cond))
        (block ,then)
    });
};

mac if (cond then else) {
    ret \(block base.-blockIf () {
        (block {
            (exit (! ,cond))
            (block ,then)
            (exit base.-blockIf true)
        })
        (block ,else)
    });
};

mac while (cond body) {
    ret \(block base.-blockLoop () {
        (block base.-blockLoopInner () {
            (exit base.-blockLoop (! ,cond))
            (block ,body)
        })
        (loop true)
    });
};

mac for (init cond step body) {
    ret \(block base.-blockLoop () {
        ,init
        (block {
            (block base.-blockLoopInner () {
                (exit base.-blockLoop (! ,cond))
                (block ,body)
            })
            ,step
            (loop true)
        })
    });
};

mac break () {
    ret \(exit base.-blockLoop true);
};

mac continue () {
    ret \(exit base.-blockLoopInner true);
};

mac range (i up::preprocess body) {
    ret \(for (sym (,i 0:(typeOf up))) (< ,i ,up) (= ,i (+ ,i 1)) ,body);
};

mac rangeRev (i up::preprocess body) {
    sym (s (genSym));

    ret \(range ,s ,up {
        (sym (,i (- ,up 1 ,s)))
        (block ,body)
    });
};

mac range (i lo::preprocess hi::preprocess body) {
    ret \(for (sym (,i ,lo)) (<= ,i ,hi) (= ,i (+ ,i 1)) ,body);
};

mac rangeRev (i hi::preprocess lo::preprocess body) {
    sym (s (genSym));

    ret \(range ,s ,lo ,hi {
        (sym (,i (+ ,lo (- ,hi ,s))))
        (block ,body)
    });
};

mac repeat (n::preprocess body) {
    sym (s (genSym));
    ret \(range ,s ,n ,body);
};

mac ++ (x::preprocess) {
    ret \(= ,x (+ ,x 1));
};

mac -- (x::preprocess) {
    ret \(= ,x (- ,x 1));
};

mac ?_ (cond::preprocess onTrue::preprocess onFalse::preprocess) {
    ret \(block base.-blockTernSel ,(typeOf onTrue) {
        (if ,cond {
            (pass base.-blockTernSel ,onTrue)
        })
        (pass ,onFalse)
    });
};

mac += (x::preprocess y::preprocess) {
    ret \(= ,x (+ ,x ,y));
};

mac -= (x::preprocess y::preprocess) {
    ret \(= ,x (- ,x ,y));
};

mac *= (x::preprocess y::preprocess) {
    ret \(= ,x (* ,x ,y));
};

mac /= (x::preprocess y::preprocess) {
    ret \(= ,x (/ ,x ,y));
};

mac %= (x::preprocess y::preprocess) {
    ret \(= ,x (% ,x ,y));
};

mac <<= (x::preprocess y::preprocess) {
    ret \(= ,x (<< ,x ,y));
};

mac >>= (x::preprocess y::preprocess) {
    ret \(= ,x (>> ,x ,y));
};

mac &= (x::preprocess y::preprocess) {
    ret \(= ,x (& ,x ,y));
};

mac |= (x::preprocess y::preprocess) {
    ret \(= ,x (| ,x ,y));
};

mac ^= (x::preprocess y::preprocess) {
    ret \(= ,x (^ ,x ,y));
};

mac -> (x::preprocess m rest::variadic) {
    sym (ops (+ \(,x ,m) rest));

    sym (code \(passthrough ,(. ops 0)));
    range i 1 (- (lenOf ops) 1) {
        = code \(. (* ,code) ,(. ops i));
    };

    ret code;
};

mac && (a b rest::variadic) {
    sym (ops (+ \(,a ,b) rest));

    sym (innerCode {});
    range i (lenOf ops) {
        += \innerCode \\{
            (block {
                (exit ,(. ops i))
                (pass base.-blockLogAnd false)
            })};
    };
    += \innerCode \\{ (pass true) };

    ret \(block base.-blockLogAnd bool ,innerCode);
};

mac || (a b rest::variadic) {
    sym (ops (+ \(,a ,b) rest));

    sym (innerCode {});
    range i (lenOf ops) {
        += \innerCode \\{
            (block {
                (exit (! ,(. ops i)))
                (pass base.-blockLogAnd true)
            })};
    };
    += \innerCode \\{ (pass false) };

    ret \(block base.-blockLogAnd bool ,innerCode);
};

mac arr (ty::preprocess a::preprocess rest::(variadic preprocess)) {
    sym (els (+ \(,a) rest));
    sym (arrTy (ty (lenOf els)));

    sym (innerCode \{ sym res:arrTy; });
    range i (lenOf els) {
        += \innerCode \\{ (= ([] res ,i) ,(. els i)) };
    };
    += \innerCode \\{ (pass res) };

    ret \(block ,arrTy ,innerCode);
};
