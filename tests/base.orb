// TODO revisit when first-class ids introduced
// TODO rename when packages introduced (or whatever else i settle for, i've no idea where all this is going)
// or just make hygenic in some way
mac loopingBlocksName() _loopingBlocksName;
mac ifOuterBlockName() _ifOuterBlockName;
mac forFirstStepTracker() _forFirstStepTracker;

mac if(cond then) (
    block (ifOuterBlockName) () {
        exit (! cond);
        block () then;
    }
);

mac if(cond then else) (
    block (ifOuterBlockName) () {
        block () {
            exit (! cond);
            block () then;
            exit (ifOuterBlockName) true;
        };
        block () else;
    }
);

mac for(init cond step) (
    block () {
        init
        (block (loopingBlocksName) () {
            (exit (! cond))
            step
            (loop true)
        })
    }
);

mac for(init cond step body) (
    block () {
        init
        (let ((forFirstStepTracker):bool true))
        (block (loopingBlocksName) () {
            (if (forFirstStepTracker) {
                (= (forFirstStepTracker) false)
            } {
                step
            })
            (exit (! cond))
            (block () body)
            (loop true)
        })
    }
);

mac while(cond) (
    block (loopingBlocksName) () {
        loop cond;
    }
);

mac while(cond body) (
    block (loopingBlocksName) () {
        exit (! cond);
        block () body;
        loop true;
    }
);

mac do(body cond) (
    block (loopingBlocksName) () {
        block () body;
        exit (! cond);
        loop true;
    }
);

mac break() (exit (loopingBlocksName) true);

mac break(cond) (exit (loopingBlocksName) cond);

mac continue() (loop (loopingBlocksName) true);

mac continue(cond) (loop (loopingBlocksName) cond);